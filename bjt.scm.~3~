(define (bjt-active-beta-model VBEthreshold VCEsaturation beta
                               #!optional given-name)
  (3-terminal-device '(bjt-active-beta-model b c e) given-name
    (lambda (VBE IB VCE IC)
      (let-cells (type                ;+ for NPN, - for PNP
                  VBEsign VCEsign IBsign ICsign
                  absVBEthreshold absVCEsaturation
		  absVBE absVCE absIC alpha IE
                  (false #f))

        (sign VBEthreshold type)

        (same VBEsign type)
        (sign VBE VBEsign)

        (same VCEsign type)
        (sign VCE VCEsign)

        (same IBsign type)
        (sign IB IBsign)

        (same ICsign type)
        (sign IC ICsign)

        (absolute-value VBEthreshold absVBEthreshold)
        (absolute-value VBE absVBE)

        (absolute-value VCEsaturation absVCEsaturation)

        ;; Active transistor
        (<=? absVCE absVCEsaturation false)    ;Not saturated
        (same VBE VBEthreshold)                ;Not cutoff
        (product beta IB IC)
	(product alpha IE IC)

	))
    VBEthreshold VCEsaturation beta))

(define (bjt-active-EberMoll-model VBEthreshold VCEsaturation beta IS
                                   #!optional given-name)
  (3-terminal-device '(bjt-active-EberMoll-model b c e) given-name
    (lambda (VBE IB VCE IC)
      (let-cells (type                ;+ for NPN, - for PNP
                  VBEsign VCEsign IBsign ICsign
                  absVBEthreshold absVCEsaturation
		  absVBE absVCE absIC alpha IE
                  (false #f))

        (sign VBEthreshold type)

        (same VBEsign type)
        (sign VBE VBEsign)

        (same VCEsign type)
        (sign VCE VCEsign)

        (same IBsign type)
        (sign IB IBsign)

        (same ICsign type)
        (sign IC ICsign)

        (absolute-value VBEthreshold absVBEthreshold)
        (absolute-value VBE absVBE)

        (absolute-value VCEsaturation absVCEsaturation)

        ;; Active transistor
        (<=? absVCE absVCEsaturation false)    ;Not saturated
        ((exponential-diode IS q/kT) VBE IE)
        (product beta IB IC)
	(product alpha IE IC)

	))
    VBEthreshold VCEsaturation beta IS))

(define (exponential-diode IS q/kT #!optional given-name)
  (2-terminal-device '(exponential-diode anode cathode) given-name
    (lambda (v i)
      (let-cells (v*q/kT e^v*q/kT e^v*q/kT-1 (one 1))
        (product v q/kT v*q/kT)
        (c:exp v*q/kT e^v*q/kT)
        (sum one e^v*q/kT-1 e^v*q/kT)
        (product e^v*q/kT-1 IS i)))
    IS q/kT))

#|
;;; Beta model of common-emitter amplifier.

(initialize-scheduler)

(define TOP (node 'TOP))
(define GND (node 'GND))

(define B (node 'B))
(define C (node 'C))
(define E (node 'E))

(define-cell VThreshold)
(define-cell VSaturation)
(define-cell beta)

(define Q
  ((bjt-active-beta-model VThreshold VSaturation beta 'Q)
   B C E))

(define-cell RB1)
(define RBU ((linear-resistor RB1 'RB1) TOP B))

(define-cell RB2)
(define RBD ((linear-resistor RB2 'RB2) B GND))

(define-cell RC)
(define RPU ((linear-resistor RC 'RC) TOP C))

(define-cell RE)
(define RPD ((linear-resistor RE 'RE) E GND))

(define-cell VCC)
(define VS  ((voltage-source VCC 'VCC) TOP GND))

(cap! TOP)
(cap! GND)
(cap! B)
(cap! C)
(cap! E)

;;; E96 1% values

(tell! VCC 15 'gjs1)
(tell! (potential GND) 0 'gjs2)

(tell! RB2 20000  'gjs3)
(tell! RB1 162000 'gjs4)
(tell! RC  4990   'gjs5)
(tell! RE  1000   'gjs6)

(tell! VThreshold 0.65 'gjs7)		
(tell! VSaturation 0.2 'gjs7)
(tell! beta 100 'gjs8)

(cpp (inquire (thing '(current c Q))))

|#
